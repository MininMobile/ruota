dynamic Curses :: {
	height := 0;
	width := 0;
	buffer := [];
	screen := [];
	run := true;
	init(width, height) => {
		os.system("cls");
		self.height = height;
		self.width = width;
		buffer = alloc width;
		screen = alloc width;
		i in buffer ->> i = alloc height;
		i in screen ->> i = alloc height;
		clear();
	};

	setString(x, y, s, v) => {
		i in [0 : len s] ->> {
			setChar(x + i, y, s[i], v); 
		};
	};

	setChar(x, y, c, v) => {
		buffer[x][y] != c then {
			buffer[x][y] = c;
			v then printat(x + 1, y + 1, c);
		};
	};

	clear() => {
		i in [0 : width] ->> {
			j in [0 : height] ->> {
				buffer[i][j] = ' ';
				printat(i + 1, j + 1, ' ');
			};
		};
	};

	close() => {
		run = false;
	};

	render() => {
		detach run do {
			i in [0 : width] ->> {
				j in [0 : height] ->> {
					screen[i][j] != buffer[i][j] then {
						printat(i + 1, j + 1, buffer[i][j]);
						screen[i][j] = buffer[i][j];
					}
				};
			};
		};
	};
};